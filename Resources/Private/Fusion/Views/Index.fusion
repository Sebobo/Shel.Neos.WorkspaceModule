Shel.Neos.WorkspaceModule.WorkspacesController {
    index = Neos.Fusion:Component {
        userWorkspace = ${userWorkspace}
        baseWorkspaceOptions = ${baseWorkspaceOptions}
        ownerOptions = ${ownerOptions}
        workspaces = ${workspaces}
        csrfToken = ${csrfToken}
        userCanManageInternalWorkspaces = ${userCanManageInternalWorkspaces}

        endpoints = Neos.Fusion:DataStructure {
            deleteWorkspace = Neos.Fusion:UriBuilder {
                action = 'delete'
                arguments.workspace = '---workspace---'
            }
            editWorkspace = Neos.Fusion:UriBuilder {
                action = 'edit'
                arguments.workspace = '---workspace---'
            }
            updateWorkspace = Neos.Fusion:UriBuilder {
                action = 'update'
                format = 'json'
            }
            createWorkspace = Neos.Fusion:UriBuilder {
                action = 'create'
            }
            newWorkspace = Neos.Fusion:UriBuilder {
                action = 'new'
            }
            showWorkspace = Neos.Fusion:UriBuilder {
                action = 'show'
                arguments.workspace = '---workspace---'
            }
            getChanges = Neos.Fusion:UriBuilder {
                action = 'getChanges'
                format = 'json'
            }
        }

        renderer = afx`
            <div class="neos-content neos-container-fluid">
                <div class="neos-row-fluid">
                    <div
                        id="workspace-module-app"
                        data-endpoints={Json.stringify(props.endpoints)}
                        data-user-workspace={props.userWorkspace.name}
                        data-csrf-token={props.csrfToken}
                        data-user-can-manage-internal-workspaces={props.userCanManageInternalWorkspaces ? 'true' : 'false'}
                    >
                        <script type="application/json" id="workspaces">
                            {Json.stringify(props.workspaces)}
                        </script>
                        <script type="application/json" id="baseWorkspaceOptions">
                            {Json.stringify(props.baseWorkspaceOptions)}
                        </script>
                        <script type="application/json" id="ownerOptions">
                            {Json.stringify(props.ownerOptions)}
                        </script>
                        Loading workspace module...
                    </div>
                </div>
                <div class="neos-footer">
                    <a class="neos-button neos-button-success" href={props.endpoints.newWorkspace}>
                        Create new workspace
                    </a>
                </div>
            </div>
        `
    }
}
